plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.athenhub'
version = '0.0.1-SNAPSHOT'
description = 'project-interface'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mockitoAgent
}

repositories {
    mavenCentral()

    maven {
        name = "GitHubPublicPackages"
        url = uri("https://maven.pkg.github.com/athenhub/common")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}
ext {
    set('springCloudVersion', "2025.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // docker-compose
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // loki
    implementation 'com.github.loki4j:loki-logback-appender:2.0.0'

    // zipkin
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

    // spring cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // PostgreSQL
    implementation 'org.postgresql:postgresql'

    // common
    implementation 'com.athenhub:common-mvc:1.2.0'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    // archunit
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.4.1'

    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // mockito javaagent
    mockitoAgent('org.mockito:mockito-core') {
        transitive = false
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs "-javaagent:${configurations.mockitoAgent.asPath}"
}

checkstyle {
    toolVersion = '12.1.2'
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.named('check') {
    dependsOn 'checkstyleMain', 'checkstyleTest'
}

spotless {
    java {
        googleJavaFormat()
        target 'src/**/*.java'
    }
}

tasks.check {
    dependsOn 'spotlessCheck'
}
